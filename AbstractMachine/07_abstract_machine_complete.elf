%%--------------------------------------------------------------
%% Plugging the expressions in the stacks/evaluation contexts
%%--------------------------------------------------------------

plug : exp -> stack -> exp -> type.
%mode plug +E +S -E2.

plug/nil : plug E nil E.
plug/s : plug E (f-s , S) E'
	  <- plug (s E) S E'.
plug/fst : plug E (f-fst , S) E'
	    <- plug (fst E) S E'.
plug/snd : plug E (f-snd , S) E'
	    <- plug (snd E) S E'.
plug/pair1 : plug E (f-pair1 E2 , S) E'
	    <- plug (pair E E2) S E'.
plug/pair2 : plug E (f-pair2 (V : value E1) , S) E'
	      <- plug (pair E1 E) S E'.
plug/app1 : plug E (f-app1 E2 , S) E'
	     <- plug (app E E2) S E'.
plug/app2 : plug E (f-app2 (V : value F) , S) E'
	    <- plug (app F E) S E'.

%worlds () (plug _ _ _).
%total S (plug _ S _).

%%-------------------------------------------------------------
%% The same as above, but this this we output both the term and
%% a proof that relates the output to the inputs (AKA strong specification.
%%--------------------------------------------------------------

get-plug : {E : exp}{S : stack} {E' : exp} plug E S E' -> type.
%mode get-plug +E +S -E' -PLUG.

- : get-plug E nil E plug/nil.
- : get-plug E (f-s , S) E' (plug/s D)
     <- get-plug (s E) S E' D.
- : get-plug E (f-fst , S) E' (plug/fst D)
     <- get-plug (fst E) S E' D.
- : get-plug E (f-snd , S) E' (plug/snd D)
     <- get-plug (snd E) S E' D.
- : get-plug E (f-pair1 E2 , S) E' (plug/pair1 D)
     <- get-plug (pair E E2) S E' D.
- : get-plug E (f-pair2 (V : value E1) , S) E' (plug/pair2 D)
     <- get-plug (pair E1 E) S E' D.
- : get-plug E (f-app1 E2 , S) E' (plug/app1 D)
     <- get-plug (app E E2) S E' D.
- : get-plug E (f-app2 (V : value F) , S) E' (plug/app2 D)
     <- get-plug (app F E) S E' D.

%worlds () (get-plug _ _ _ _).
%total S (get-plug _ S _ _).

%%---------------------------------------------------------
%% Flattenning the states into expressions

flat : state -> exp -> type.
%mode flat +S -E.

flat/ev : flat (ev E S) E'
	   <- plug E S E'.

flat/ap : flat (ap (V : value E) S) E'
	   <- plug E S E'.

%worlds () (flat _ _).
%total S (flat S _).

%%-------------------------------------------------------

get-flat : {ST : state} {E : exp} flat ST E -> type.
%mode get-flat +S -E -FLAT.

- : get-flat (ev E1 S) E (flat/ev LEMMA)
     <- get-plug E1 S E LEMMA.

- : get-flat (ap (V : value E1) S) E (flat/ap LEMMA)
     <- get-plug E1 S E LEMMA.

%worlds () (get-flat _ _ _).
%total S (get-flat S _ _).

%%--------------------------------------------------------
%% Lemma: result of plug is unique

plug-eq : plug E S X1 -> plug E S X2 -> id-exp X1 X2 -> type.

%mode plug-eq +P1 +P2 -EQ.

- : plug-eq plug/nil plug/nil id-exp/refl.

- : plug-eq (plug/s P1) (plug/s P2) EQ
     <- plug-eq P1 P2 EQ.

- : plug-eq (plug/fst P1) (plug/fst P2) EQ
    <- plug-eq P1 P2 EQ.

- : plug-eq (plug/snd P1) (plug/snd P2) EQ
    <- plug-eq P1 P2 EQ.

- : plug-eq (plug/pair1 P1) (plug/pair1 P2) EQ
    <- plug-eq P1 P2 EQ.

- : plug-eq (plug/pair2 P1) (plug/pair2 P2) EQ
    <- plug-eq P1 P2 EQ.

- : plug-eq (plug/app1 P1) (plug/app1 P2) EQ
    <- plug-eq P1 P2 EQ.

- : plug-eq (plug/app2 P1) (plug/app2 P2) EQ
    <- plug-eq P1 P2 EQ.

%worlds () (plug-eq _ _ _).
%total P1 (plug-eq P1 _ _).

%% Result of flat is unique : 

flat-eq : flat S E1 -> flat S E2 -> id-exp E1 E2 -> type.

%mode flat-eq +P1 +P2 -EQ.

- : flat-eq (flat/ev S1) (flat/ev S2) GOAL
     <- plug-eq S1 S2 GOAL.

- : flat-eq (flat/ap S1) (flat/ap S2) GOAL
     <- plug-eq S1 S2 GOAL.

%worlds () (flat-eq _ _ _).
%total P1 (flat-eq P1 _ _).

%%-----------------------------------------------------------
%% Reflexive closure of the reduction relation
%%-----------------------------------------------------------

stronger : exp -> exp -> type.
%mode stronger +E1 -E2.

% reduction rules
str/fst-pair : stronger (fst (pair E1 E2)) E1
		<- value E2
		<- value E1.
str/snd-pair : stronger (snd (pair E1 E2)) E2
		<- value E2
		<- value E1.
str/beta : stronger (app (lam F) E) E'
	    <- id-exp (F E) E'
	    <- value E.

% congruence rules
str/z : stronger z z.
str/s : stronger (s E1) (s E2) <- stronger E1 E2.
str/fst : stronger (fst E1) (fst E2) <- stronger E1 E2.
str/snd : stronger (snd E1) (snd E2) <- stronger E1 E2.
str/pair : stronger (pair E1 E2) (pair E1' E2')
	    <- stronger E2 E2'
	    <- stronger E1 E1'.
str/lam : stronger (lam F) (lam F).
str/app : stronger (app E1 E2) (app E1' E2')
	    <- stronger E2 E2'
	    <- stronger E1 E1'.

%worlds () (stronger _ _).
%terminates E (stronger E _).

%%---------------------------------------------

strong-refl : {E : exp} stronger E E -> type.
%mode strong-refl +E -STRONGER.

- : strong-refl z str/z.
- : strong-refl (s E) (str/s D) <- strong-refl E D.
- : strong-refl (fst E) (str/fst D) <- strong-refl E D.
- : strong-refl (snd E) (str/snd D) <- strong-refl E D.
- : strong-refl (lam F) str/lam.
- : strong-refl (pair E1 E2) (str/pair D1 D2)
     <- strong-refl E1 D1
     <- strong-refl E2 D2.
- : strong-refl (app E1 E2) (str/app D1 D2)
     <- strong-refl E1 D1
     <- strong-refl E2 D2.

%worlds () (strong-refl _ _).
%total E (strong-refl E _).

%%-----------------------------------------------------
%% A technical lemma, so that we can stop Twelf from
%% using higher-order unification (e.g. F E = fst X)
%%-----------------------------------------------------

strong-eval-lemma:
		 value E
		 -> id-exp (F E) E'
		 -> eval E' V
		 -> eval (app (lam F) E) V
		 -> type.
%mode strong-eval-lemma +V +EQ +EV2 -EV.

- : strong-eval-lemma V id-exp/refl EV (eval/app EV ARG eval/lam)
     <- eval/val V ARG.

%worlds () (strong-eval-lemma _ _ _ _).
%total {} (strong-eval-lemma _ _ _ _).

%%-------------------------------------------------
%% E1 >= E2 and  eval E2 V implies eval E1 V
%%-------------------------------------------------

strong-eval : stronger E1 E2 -> eval E2 V -> eval E1 V -> type.
%mode strong-eval +IMP +EV2 -EV1.

- : strong-eval (str/beta V EQ) EV2 GOAL
     <- strong-eval-lemma V EQ EV2 GOAL.

- : strong-eval (str/fst-pair V1 V2) EV2 (eval/fst (eval/pair OUT EV2))
     <- eval/val V2 OUT.

- : strong-eval (str/snd-pair V1 V2) EV2 (eval/snd (eval/pair EV2 P2))
     <- eval/val V1 P2.

- : strong-eval str/z E E.

- : strong-eval (str/s IMP) (eval/s D) (eval/s D2)
     <- strong-eval IMP D D2.

- : strong-eval (str/fst IMP) (eval/fst D) (eval/fst D2)
     <- strong-eval IMP D D2.

- : strong-eval (str/snd IMP) (eval/snd D) (eval/snd D2)
     <- strong-eval IMP D D2.

- : strong-eval (str/pair B A) (eval/pair D1 D2) (eval/pair D1' D2')
     <- strong-eval A D1 D1'
     <- strong-eval B D2 D2'.

- : strong-eval str/lam E E.

- : strong-eval (str/app B A) (eval/app RES2 ARG FUN) (eval/app RES2 ARG2 FUN2)
     <- strong-eval B FUN FUN2
     <- strong-eval A ARG ARG2.

%worlds () (strong-eval _ _ _).
%total I (strong-eval I _ _).

%%--------------------------------------------------------

stronger-ctx : {S : stack}
	   stronger E1 E2
	   -> plug E1 S E1'
	   -> plug E2 S E2'
	   -> stronger E1' E2'
	   -> type.
%mode stronger-ctx +S +IMP +P1 +P2 -IMP2.

- : stronger-ctx nil IMP plug/nil plug/nil IMP.

- : stronger-ctx (f-s , S) IMP (plug/s P1) (plug/s P2) LEMMA
     <- stronger-ctx S (str/s IMP) P1 P2 LEMMA.

- : stronger-ctx (f-fst , S) IMP (plug/fst P1) (plug/fst P2) LEMMA
     <- stronger-ctx S (str/fst IMP) P1 P2 LEMMA.

- : stronger-ctx (f-snd , S) IMP (plug/snd P1) (plug/snd P2) LEMMA
     <- stronger-ctx S (str/snd IMP) P1 P2 LEMMA.

- : stronger-ctx (f-pair1 E , S) IMP (plug/pair1 P1) (plug/pair1 P2) LEMMA
     <- strong-refl E IMP2
     <- stronger-ctx S (str/pair IMP IMP2) P1 P2 LEMMA.

- : stronger-ctx (f-pair2 (V : value E) , S) IMP (plug/pair2 P1) (plug/pair2 P2)
     LEMMA
     <- strong-refl E IMP2
     <- stronger-ctx S (str/pair IMP2 IMP) P1 P2 LEMMA.

- : stronger-ctx (f-app1 E , S) IMP (plug/app1 P1) (plug/app1 P2) LEMMA
     <- strong-refl E IMP2
     <- stronger-ctx S (str/app IMP IMP2) P1 P2 LEMMA.

- : stronger-ctx (f-app2 (V : value E) , S) IMP (plug/app2 P1) (plug/app2 P2) LEMMA
     <- strong-refl E IMP2
     <- stronger-ctx S (str/app IMP2 IMP) P1 P2 LEMMA.

%worlds () (stronger-ctx _ _ _ _ _).
%total S (stronger-ctx S _ _ _ _).

%%------------------------------------------------------------------
%% The invariant of the transitions:
%% - if the end state evaluates then the starting state does so too.
%%-----------------------------------------------------------------

%% This could be a bit clearer (but of the same length)

% inv2 : ST1 =AM=> ST2
%        -> flat ST1 E1
%        -> flat ST2 E2
%        -> stronger E1 E2
%        -> type.

% %mode inv2 +STEP +FLAT1 +FLAT2 -STRONGER.

% - : inv2 ev/z (flat/ev P1) (flat/ap P2) GOAL
%      <- plug-eq P2 P1 EQ.

% %worlds () (inv2 _ _ _ _).
% %total {} (inv2 _ _ _ _).

%%-----------------------------------------------------

inv : ST1 =AM=> ST2
       -> flat ST1 E1
       -> flat ST2 E2
       -> eval E2 V
       -> eval E1 V
       -> type.

%mode inv +STEP +FLAT1 +FLAT2 +EVAL2 -EVAL1.

- : inv ev/z (flat/ev P1) (flat/ap P2) EV GOAL
     <- plug-eq P2 P1 EQ
     <- eval-equiv-arg EV EQ GOAL.

- : inv ev/s (flat/ev P1) (flat/ev (plug/s P2)) EV EV2
     <- plug-eq P2 P1 EQ
     <- eval-equiv-arg EV EQ EV2.

- : inv ap/s (flat/ap (plug/s P1)) (flat/ap P2) EV EV2
     <- plug-eq P2 P1 EQ
     <- eval-equiv-arg EV EQ EV2.

- : inv ev/fst (flat/ev P1) (flat/ev (plug/fst P2)) EV EV2
     <- plug-eq P2 P1 EQ
     <- eval-equiv-arg EV EQ EV2.

% %% Non-trivial case, because we contract a redex here
% %% fst (V1 V2) ---> V1

-  : inv (ap/fst : ap (val/pair V1 V2) _ =AM=> _)
      (flat/ap (plug/fst P1)) (flat/ap P2) EV EV3
      <- stronger-ctx _ (str/fst-pair V1 V2) P1 P2 GOAL
      <- strong-eval GOAL EV EV3.

- : inv ev/snd (flat/ev P1) (flat/ev (plug/snd P2)) EV EV2
     <- plug-eq P2 P1 EQ
     <- eval-equiv-arg EV EQ EV2.

% %% Here also a redex is contracted
% %% fst (V1 V2) ---> V2
% %% - : inv (ap/snd) F1 F2 EV2 EV23.

-  : inv (ap/snd : ap (val/pair V1 V2) _ =AM=> _)
      (flat/ap (plug/snd P1)) (flat/ap P2) EV EV3
      <- stronger-ctx _ (str/snd-pair V1 V2) P1 P2 GOAL
      <- strong-eval GOAL EV EV3.

- : inv ev/pair1 (flat/ev P1) (flat/ev (plug/pair1 P2)) EV EV2
     <- plug-eq P2 P1 EQ
     <- eval-equiv-arg EV EQ EV2.

- : inv ap/pair1 (flat/ap (plug/pair1 P1)) (flat/ev (plug/pair2 P2)) EV EV2
     <- plug-eq P2 P1 EQ
     <- eval-equiv-arg EV EQ EV2.

- : inv ap/pair2 (flat/ap (plug/pair2 P1)) (flat/ap P2) EV EV2
     <- plug-eq P2 P1 EQ
     <- eval-equiv-arg EV EQ EV2.

- : inv ev/lam (flat/ev P1) (flat/ap P2) EV EV2
     <- plug-eq P2 P1 EQ
     <- eval-equiv-arg EV EQ EV2.

- : inv ev/app1 (flat/ev P1) (flat/ev (plug/app1 P2)) EV EV2
     <- plug-eq P2 P1 EQ
     <- eval-equiv-arg EV EQ EV2.

- : inv ap/app1 (flat/ap (plug/app1 P1)) (flat/ev (plug/app2 P2)) EV EV2
     <- plug-eq P2 P1 EQ
     <- eval-equiv-arg EV EQ EV2.

%% The third and final non-trivial case,
%% A beta reduction is performed here.

- : inv (ap/app2 : ap (V : value E) _ =AM=> _)
     (flat/ap (plug/app2 P1)) (flat/ev P2) EV EV2
     <- stronger-ctx _ (str/beta V id-exp/refl) P1 P2 GOAL
     <- strong-eval GOAL EV EV2.

%worlds () (inv _ _ _  _ _).
%total STEP (inv STEP _ _ _ _).

%%--------------------------------------------------
%% Invariant on the many-step relation
%%--------------------------------------------------

inv-many : ST1 ===>+ ST2
	    -> flat ST1 E1
	    -> flat ST2 E2
	    -> eval E2 V
	    -> eval E1 V
	    -> type.

%mode inv-many +STEP +FLAT1 +FLAT2 +EVAL2 -EVAL1.

- : inv-many am-many/z F1 F2 EV GOAL
     <- flat-eq F2 F1 EQ
     <- eval-equiv-arg EV EQ GOAL.

- : inv-many (am-many/s STEPS (STEP : _ =AM=> ST3)) F1 F2 EV GOAL
     <- get-flat ST3 E3 F3
     <- inv-many STEPS F3 F2 EV LEMMA
     <- inv STEP F1 F3 LEMMA GOAL.

%worlds () (inv-many _ _ _  _ _).
%total STEP (inv-many STEP _ _ _ _).

%%----------------------------------------------------
%% COMPLETENESS OF THE ABSTRACT MACHINE
%%----------------------------------------------------

complete:
	    ev E nil ===>+ ap V nil
	    -> eval E V
	    -> type.

%mode complete +STEPS -EVAL.

- : complete STEPS (GOAL : eval E V)
     <- eval/val V EV
     <- inv-many STEPS
	(flat/ev plug/nil)
	(flat/ap plug/nil)
	EV
	GOAL.

%worlds () (complete _ _).
%total STEPS (complete STEPS _ ).
